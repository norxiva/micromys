buildscript {
    ext.dependencyPluginVersion = '1.0.3.RELEASE'
    ext.springBootVersion = '1.5.13.RELEASE'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyPluginVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

group 'my.norxiva'
version '1.0-SNAPSHOT'

allprojects {
    ext.axonVersion = '3.2.2'
    ext.bouncycastleVersion = '1.59'

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    apply plugin: 'io.spring.dependency-management'
    // plugin 'spring-boot' is deprecated instead of 'org.springframework.boot'
    apply plugin: 'org.springframework.boot'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:Brussels-SR10'
            // axon 3.2.2 doesn't support spring cloud Dalston.SR5
            // but if use Edgware.SR3(eureka-client version is 1.4.4) has a bug:
            // it will take effect automatically if spring-cloud-starter-eureka is included
            // and @EnableDiscoveryClient is not set.
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Edgware.SR3'
        }
    }

    configurations {
        all*.exclude module: 'spring-boot-starter-tomcat'
    }

    repositories {
        mavenCentral()
    }
}
